plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'xyz.jpenilla.run-paper' version '1.0.6'
}

tasks {
    shadowJar {
        archiveFileName = 'Chatty.jar'
        destinationDirectory.set(new File(rootProject.projectDir.getAbsolutePath() + "/build/libs"))
        relocate 'ru.mrbrikster.baseplugin', "${rootProject.group}.${rootProject.name}.shaded.baseplugin"
        relocate 'org.bstats', "${rootProject.group}.${rootProject.name}.shaded.metrics"
        relocate 'com.google.gson', "${rootProject.group}.${rootProject.name}.shaded.gson"
    }

    build {
        dependsOn(shadowJar)
    }

    runServer {
        minecraftVersion('1.18.2')
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

dependencies {
    api project(':api')
    api 'com.github.Brikster:BasePlugin:v1.8'
    api 'com.google.code.gson:gson:2.8.5'
    api 'org.bstats:bstats-bukkit:2.2.1'

    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'
    compileOnly 'me.clip:placeholderapi:2.10.6'
    compileOnly 'commons-io:commons-io:2.7'
    compileOnly 'com.nametagedit:nametagedit:4.4.16'
}