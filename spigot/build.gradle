plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'xyz.jpenilla.run-paper' version '2.2.2'
}

tasks {
    shadowJar {
        archiveFileName = "Chatty-${rootProject.version}.jar"
        destinationDirectory = rootProject.layout.buildDirectory.dir('libs')
        def relocations = ['org.bstats',
                           'com.google.gson',
                           'com.google.guava',
                           'com.google.common',
                           'org.yaml.snakeyaml',
                           'cloud.commandframework',
                           'eu.okaeri',
                           'com.zaxxer',
                           'net.kyori']
        relocations.forEach {
            relocate it, "ru.brikster.chatty.shaded.${it}"
        }
    }

    build {
        dependsOn(shadowJar)
    }

    runServer {
        minecraftVersion('1.20.2')
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

repositories {
    maven { url = 'https://jitpack.io' }
    maven { url = "https://storehouse.okaeri.eu/repository/maven-public/" }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public' }
}

def okaeriVersion = 'e1fcfc124b'
def cloudVersion = '1.8.4'

//noinspection VulnerableLibrariesLocal
dependencies {
    api project(':api')
    api 'com.google.code.gson:gson:2.9.0'
    api 'org.bstats:bstats-bukkit:3.0.0'

    implementation "com.github.Brikster.okaeri-configs:okaeri-configs-yaml-snakeyaml:$okaeriVersion"
    implementation "com.github.Brikster.okaeri-configs:okaeri-configs-serdes-commons:$okaeriVersion"
    implementation "com.github.Brikster.okaeri-configs:okaeri-configs-serdes-bukkit:$okaeriVersion"
    implementation "com.github.Brikster.okaeri-configs:okaeri-configs-validator-okaeri:$okaeriVersion"

    implementation "cloud.commandframework:cloud-core:$cloudVersion"
    implementation "cloud.commandframework:cloud-bukkit:$cloudVersion"
    implementation "cloud.commandframework:cloud-minecraft-extras:$cloudVersion"

    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'org.flywaydb:flyway-core:9.22.3'

    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'
    compileOnly 'me.clip:placeholderapi:2.10.6'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'commons-io:commons-io:2.11.0'
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = [
            'version': project.version
    ]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })
project.idea.project.settings.taskTriggers.afterSync generateTemplates
