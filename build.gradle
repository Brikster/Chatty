allprojects {
    group = 'ru.mrbrikster'
    version = '2.19.8'
}

subprojects {
    pluginManager.apply('java-library')

    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

    tasks {
        compileJava {
            options.encoding = 'UTF-8'
        }

        compileTestJava {
            options.encoding = 'UTF-8'
        }
    }

    repositories {
        mavenCentral()
        maven { url = "https://jitpack.io" }
        maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/" }
        maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
        maven { url = "https://repo.codemc.org/repository/maven-public" }
        maven { url = "https://ci.nametagedit.com/plugin/repository/everything/"; allowInsecureProtocol = true }

        maven {
            url "https://gitlab.rednite.net/api/v4/projects/549/packages/maven"
            name "shkolakola-public"

            if (shkolakolaGitlabToken) {
                credentials(HttpHeaderCredentials) {
                    name = 'Private-Token'
                    value = shkolakolaGitlabToken
                }
            } else {
                credentials(HttpHeaderCredentials) {
                    name = 'Job-Token'
                    value = System.getenv("CI_JOB_TOKEN")
                }
            }

            authentication {
                create("header", HttpHeaderAuthentication)
            }
        }
    }

    dependencies {
        compileOnlyApi 'org.jetbrains:annotations:17.0.0'

        compileOnly 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'

        compileOnly 'org.spigotmc:spigot-api:1.19-R0.1-SNAPSHOT'
        compileOnly 'com.destroystokyo:paper:1.19'
    }
}